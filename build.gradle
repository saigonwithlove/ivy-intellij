buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.3.2'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.15.0'
    id 'com.diffplug.spotless' version '6.20.0'
}

group 'saigonwithlove.ivy'
version '0.2.7'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.6'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
    testImplementation 'junit:junit:4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.3'
    plugins = ['com.intellij.java']
    updateSinceUntilBuild = true
}

patchPluginXml {
    sinceBuild = '213'
    untilBuild = '232.*'
}

task proGuard(type: proguard.gradle.ProGuardTask, dependsOn: buildPlugin) {
    injars "build/distributions/ivy-intellij-${project.version}.zip"
    outjars "build/distributions/ivy-intellij-${project.version}_optimized.zip"
    // It seems RxJava3 required JDK8's rt.jar to process.
    libraryjars '.gradle/java/jdk8u332-b09-jre/lib/rt.jar'
    libraryjars configurations.compileClasspath
    target '11'
    keep includedescriptorclasses: true, 'public class saigonwithlove.** { *; }'
    dontobfuscate
    dontwarn
}

spotless {
    java {
        googleJavaFormat().reflowLongStrings()
    }
}
